#version 460
#extension GL_EXT_mesh_shader: require

#include "types.h"

layout (local_size_x = 32, local_size_y = 1, local_size_z = 1) in;
layout (triangles, max_vertices = kMaxVerticesPerMeshlet, max_primitives = kMaxIndicesPerMeshlet) out;

layout (binding = 0) readonly buffer Vertices
{
    Vertex vertices[];
};

layout (binding = 1) readonly buffer Meshlets
{
    Meshlet meshlets[];
};

layout (push_constant) uniform constants
{
    mat4 vp;
    vec4 sun_pos;
    vec4 view_pos;
} pc;

out VS_OUT {
    layout (location = 0) vec2 uv;
    layout (location = 1) vec3 normal;
    layout (location = 2) vec3 tangent;
    layout (location = 3) vec3 bitangent;
    layout (location = 4) vec4 world_pos;
    layout (location = 5) flat uint meshlet_id;
    layout (location = 6) flat uint triangle_id;
} vs_out[];

void main()
{
    uint meshlet_id = gl_WorkGroupID.x;
    uint tid = gl_LocalInvocationIndex;
    uint workgroup_size = gl_WorkGroupSize.x;

    uint vertex_count = uint(meshlets[meshlet_id].vertices_count);
    uint triangle_count = uint(meshlets[meshlet_id].triangles_count);

    SetMeshOutputsEXT(vertex_count, triangle_count);

    // Each thread processes multiple vertices, striding by workgroup size
    for (uint i = tid; i < vertex_count; i += workgroup_size)
    {
        Vertex v = vertices[meshlets[meshlet_id].vertices[i]];

        vs_out[i].triangle_id = tid;
        vs_out[i].meshlet_id = meshlet_id;

        vs_out[i].uv = vec2(v.ux, v.uy);
        vs_out[i].normal = vec3(v.nx, v.ny, v.nz);
        vs_out[i].tangent = vec3(v.tx, v.ty, v.tz);
        vs_out[i].bitangent = cross(vs_out[i].tangent, vs_out[i].normal);
        vs_out[i].world_pos = vec4(v.px, v.py, v.pz, 1.0);

        gl_MeshVerticesEXT[i].gl_Position = pc.vp * vs_out[i].world_pos;
    }

    // Same for triangles
    for (uint i = tid; i < triangle_count; i += workgroup_size)
    {
        gl_PrimitiveTriangleIndicesEXT[i] = uvec3(
        meshlets[meshlet_id].indices[i * 3 + 0],
        meshlets[meshlet_id].indices[i * 3 + 1],
        meshlets[meshlet_id].indices[i * 3 + 2]
        );
    }
}
